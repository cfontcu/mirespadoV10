###################################################################
#Módulo: VDREGULARIZASTK.PAN
#Funcionalidad :  Pantalla de Regularizaciones de cantidades de Stock
#Autor: FGS      
#Fecha: 18-06-2008
###################################################################
# Histórico de cambios:
# M001 - DFL 16/07/2018
#        CUANDO HAYA UNA REGULARIZACION POR "ROTURA", COMPROBAR SI EL ARTICULO/PROVEEDOR ESTA SUJETO A 
#        FMD, DE SER ASÍ, ABRIR UNA PANTALLA PARA LEER LOS NUMEROS DE SERIE ROTOS
# -----------------------------------------------------------------------------------------------------
REGULARIZACIONES DE STOCK DESDE PANTALLAS

   Ubicación: _20___________ _20_________   
   Matrícula: __________________    
   Orden Stk: @@@@@@@@@ 
                                                                              
   Artículo:  ____________________  _40_______________________________________
   
   Lote:      ____________________  BloqLote: _________  Caducidad:  _________            
   
   Bloqueos:  __________  
   
   UnixEmb:   #L15#.##      EmbxCont:  #L###### 
   
   Actividad: _60____________________
   
   Cantidad:   #L######.### _10__    
   Reservado:  #L######.###          
   
   Regulariza: #L######  Concepto: _10________
   
   Clave Externa: _40_____________________________________   
   Observ: 
   _1024______________________________________________________________________________________
   _1024______________________________________________________________________________________
   _1024______________________________________________________________________________________
   _1024______________________________________________________________________________________
|

TABLA=VDSTOCK
PROTECT("SUPERVISOR")

POSTQUERY=FEJECUTA("CSELDATOS","ERROR EN CSELDATOS")


CAMPO=CODUBI,AUXILIAR,NOENTER
CAMPO=CODAREA,AUXILIAR,NOENTER
CAMPO=CODMAT,NOENTER
CAMPO=ORDENSTK,NOENTER
CAMPO=CODART,NOENTER
CAMPO=DESART,AUXILIAR,NOENTER
CAMPO=CODLOT,NOENTER
CAMPO=BLOQLOTE,AUXILIAR,NOENTER
CAMPO=CADUCI,AUXILIAR,NOENTER
CAMPO=BLOQUEOS,NOENTER
CAMPO=UNIEMB,NOENTER,TOOLTIP("Unidades por embalaje en unidades host")
CAMPO=EMBCONT,NOENTER
CAMPO=MARCASTK,NOENTER
CAMPO=CANTIDAD,NOENTER,TOOLTIP("Cantidad en unidades host")
CAMPO=UNIDADESHOST,AUXILIAR,NOENTER
CAMPO=RESERVADO,AUXILIAR,NOENTER
CAMPO=DIFF,PROTECT("SUPERVISOR"),AUXILIAR,TOOLTIP("Cantidad a Regularizar")
CAMPO=CODCONCE,PROTECT("SUPERVISOR"),AUXILIAR,COMBOX("CLISTACONCE"),COMBOXREADONLY,POSTCHANGE=FEJECUTA("CPROPCONCEPTO",""),
      CONTEXTUAL=FEJECUTAFORM("VDSELCONCEPTOFORM2","N","","CODART=:CODART CAMPO='CANTIDAD' CANTIDAD=:DIFF","CODCONCE=:CODCONCEENT UBICODCONCE=:FUENTE CANTMAX=:CANTMAX PVPMAX=:PVLMAX","SELECCION DE CONCEPTOS PARA REGULARIZACIONES DE CANTIDAD","S")
CAMPO=DATOSHOST,AUXILIAR
CAMPO=UBICODCONCE,AUXILIAR,OCULTO,"_12_________"
# OBSERVACIONES
CAMPO=OBSERV1,AUXILIAR
CAMPO=OBSERV2,AUXILIAR
CAMPO=OBSERV3,AUXILIAR
CAMPO=OBSERV4,AUXILIAR
CAMPO=OBSERV,AUXILIAR,OCULTO,"_4096______"
CAMPO=CANTMAX,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=PVPMAX,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=PVPDIFF,AUXILIAR,OCULTO,"@L@@@@@@@@@@@@@@@"
CAMPO=CODMOVOUT,AUXILIAR,OCULTO,"#L#######"
CAMPO=PUEDEREG,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=MSJERROR,AUXILIAR,OCULTO,"_1024____________"
CAMPO=RET,AUXILIAR,OCULTO,"@L"
#M001 INICIO
CAMPO=LEIDOS,AUXILIAR,OCULTO,"#L######"
#M001 FIN

CURSOR=CPROPCONCEPTO SELECT  FUENTE UBICODCONCE,CANTMAX,PVLMAX PVPMAX 
                      FROM VDCONCEPTOFORM 
                      WHERE TIPOPROP='CANTIDAD' AND CODCONCEENT=:CODCONCE
                     UNION
                      SELECT '',0,0 FROM DUAL WHERE :CODCONCE IS NULL;

CURSOR=CLISTACONCE SELECT CODCONCEENT,DESTIPOPROP,FUENTE,CMP.CONCEPTOINTER,CMP.SWTHOST
                      FROM VDCONCEPTOFORM CFRM,VDCONCEPTOMAP CMP,VDCONCEPTO CON
                      WHERE  TIPOPROP='CANTIDAD' AND ACTIVO='S' AND
                             (CODCLASE IS NULL OR EXISTS (SELECT 1 FROM VDCLASEARTIC CLA WHERE CLA.CODCLASIF=CFRM.CODCLASIF AND CLA.CODCLASE=CFRM.CODCLASE AND CLA.CODART=:CODART)) AND
                            (CFRM.RANGODIV IS NULL OR EXISTS (SELECT 1 FROM VDARTIC ART WHERE ART.CODART=:CODART AND VD.CONTIENECAD(CFRM.RANGODIV,ART.CODDIV)>0)) AND
                            (CMP.RANCODDIV IS NULL OR EXISTS (SELECT 1 FROM VDARTIC ART WHERE ART.CODART=:CODART AND VD.CONTIENECAD(CMP.RANCODDIV,ART.CODDIV)>0)) AND
                            CMP.CODCONCE(+)=CFRM.CODCONCEENT AND CON.CODCONCE=CFRM.CODCONCEENT AND ((CON.TIPOCONCE='E' AND :DIFF>0) OR (CON.TIPOCONCE!='E' AND :DIFF<0) OR :DIFF=0)
                      ORDER BY ORDEN; 


CURSOR=CSELDATOS SELECT LOT.BLOQLOTE, LOT.CADUCI, UBI.CODUBI, UBI.CODAREA,ART.DESART, ART.UNIDADESHOST,
                        VDART.VD2HOST(VDSTK.STKRESERVADO(:CODMAT,:ORDENSTK),:CODART) RESERVADO
                   FROM VDLOTES LOT, VDARTIC ART, VDCONTE CNT, VDUBICA UBI
                     WHERE ART.CODART = :CODART AND
                           LOT.CODART = :CODART AND
                           LOT.CODLOT = :CODLOT AND
                           CNT.CODMAT = :CODMAT AND
                           CNT.CODUBI = UBI.CODUBI;
 
CURSOR=CMOVTOSHUERFANOS SELECT :RESERVADO FROM DUAL WHERE :RESERVADO>0;

CURSOR=CDAMEOBSERV SELECT TRIM(:OBSERV1) || ' ' || TRIM(:OBSERV2) || ' ' || TRIM(:OBSERV3) || ' ' || TRIM(:OBSERV4) OBSERV FROM DUAL;

CURSOR=CDIFFESCERO SELECT :DIFF FROM DUAL WHERE :DIFF = 0;

CURSOR=CCODCONCENULO SELECT :CODCONCE FROM DUAL WHERE :CODCONCE IS NULL;

CURSOR=CSUPERACANTMAX SELECT :CANTMAX FROM DUAL WHERE :CANTMAX>0 AND ABS(:DIFF)>VDART.HOST2VD(:CANTMAX,:CODART);

CURSOR=CSUPERAPVPMAX SELECT (ART.PVDVIGENTE*ABS(:DIFF)) PVPDIFF FROM VDARTIC ART WHERE :PVPMAX>0 AND CODART=:CODART AND (ART.PVPVIGENTE*ABS(:DIFF)) > :PVPMAX;

CURSOR=CPUEDEREG SELECT 1 PUEDEREG FROM DUAL WHERE (:CANTIDAD+:DIFF)-:RESERVADO>=0; 


PRECOMMIT=FEJECUTA("+CSELDATOS","ERROR EN CSELDATOS",
                   "!-CMOVTOSHUERFANOS","Hay movtos pdtes del stock transformado.\n ¿ Desea Confirmar la Tranformación ?",                   
                   "+CDAMEOBSERV","",
                   "-CDIFFESCERO","DEBE INFORMAR LA DIFERENCIA EN CANTIDAD",
                   "-CCODCONCENULO"," DEBE INFORMAR UN CONCEPTO ",
                   "-CSUPERACANTMAX","LA CANTIDAD A REGULARIZAR NO PUEDE SUPERAR\n LA MAXIMA PERMITIDA POR ESTE CONCEPTO (:CANTMAX :UNIDADESHOST)",
                   "-CSUPERAPVPMAX","EL PRECIO DE LA REGURALIZ. ES :PVPDIFF Y SUPERA\n EL MAXIMO A REGULARIZAR (:PVPMAX), POR ESTE CONCEPTO",                 
                   "CREGULARIZA","ERROR EN CREGULARIZA",
                   "-CMSJERROR",":MSJERROR",
                   FCOMMIT,"",
                   "CUPDCLAVEEXT","",
                   FIMPRIME("INFORME","VDMOVIMIENTO.rpt","CODMOV=:CODMOVOUT",""),":V10ERROR",
                   %FFAILURE,"REGULARIZACION DE STOCK REALIZADA",
                   FIF("VERFMD",FEJECUTAFORM("VDFMDDECOM","N","","CANTIDAD=:DIFF CODART=:CODART CODLOT=:CODLOT DESART=:DESART","LEIDOS=:LEIDOS"),FSUCCESS),"",
                   FCOMMIT,"",
                   +FPULSATECLAS("ESC","F3","F2"),"")
                                  
# REGULARZA EL STOCK, Y SI TIENE ESA MATRICULA BULTO DE PICKING INVERSO ASOCIADO,
# SINCRONIZA ESTE CON EL NUEVO STOCK DEL BULTO (INCLUSO PUEDE DEJAR MOVTOS EN PDTESTOCK)
CURSOR=CREGULARIZA BEGIN
                    VDPICKINV.REGULARIZASTKPINV(:CODMAT,:ORDENSTK,:CODCONCE,:UBICODCONCE,:DIFF,:OBSERV,:CODMOVOUT,'EXP','RESERVA',:MSJERROR,:RET);
                   END;@

CURSOR=CMSJERROR SELECT :MSJERROR FROM DUAL WHERE :RET<>0;                   
   
CURSOR=CUPDCLAVEEXT UPDATE VDMOVIM SET DATOSHOST=:DATOSHOST WHERE CODMOV=:CODMOVOUT;

#M001 INICIO
CURSOR=VERFMD SELECT :CODART FROM DUAL WHERE VDFMD.TIENEFMD(:CODART, '','') = 'S' AND :CODCONCE = 'VDROT-'; 

#M001 FIN

BOTON=B1,385,265,32,32,"P",miok.png,F4,"Confirmar regularización",INCLUDECSS="background-color: transparent;border-width:0px"
BOTON=B2,425,265,32,32,"P",minook.png,ESC,"Deshacer regularización",INCLUDECSS="background-color: transparent;border-width:0px"

#TECLA=F9,FEJECUTAFORM("VDSELCONCEPTOFORM2","N","","CODART=:CODART CAMPO='CANTIDAD'","CODCONCE=:CODCONCEENT UBICODCONCE=:FUENTE CANTMAX=:CANTMAX PVPMAX=:PVLMAX","SELECCION DE CONCEPTOS PARA REGULARIZACIONES DE CANTIDAD","S")                           

