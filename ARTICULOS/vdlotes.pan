# Módulo   : VDLOTES.PAN
# Función  : Mantenimiento de lotes
#
# Creación : 01-10-2007
# Autor    : EAA
###########################################
# Histórico de cambios:
MANTENIMIENTO DE LOTES
Artículo      Designación                       Lote               Lote prov.  Caduci   Retest   Bloqueos   FMD   Libre   Bloq.   Potencia      M. Extra               Coment.   Ultima modificación
_40__________ _60______________________________ _40_______________ _40________ ________ ________ __________ _1___ @L@@,@@ @L@@,@@ #L#######.###  _ _255_______________ @L@@@@@@@ _32________ ¿D-MM-Y.YY  ________ 
|
TABLA=VDLOTES

WLONX=1400


POSTQUERY=FEJECUTA("CCALCCANT","")

PREINSERT=FEJECUTA("CSELMODIF","ERROR EN FECHA DE MODIFICACION","+CSELBLOQUEO","",
                   "CCHECKFORMATOCADUCI","EL FORMATO DE LA FECHA DE CADUCIDAD\n DEBE SER DIA,MES,AÑO (DDMMAAAA)",
                   "CSELRETEST","ERROR AL CALCULAR FECHA RETEST",
                   "+CSELPOTENCIA","")
PREUPDATE=FEJECUTA("CSELMODIF","ERROR EN FECHA DE MODIFICACION",
                   "CCHECKFORMATOCADUCI","EL FORMATO DE LA FECHA DE CADUCIDAD\n DEBE SER DIA,MES,AÑO (DDMMAAAA)",
                   "CCHECKFORMATORETEST","EL FORMATO DE LA FECHA DE RETEST\n DEBE SER DIA,MES,AÑO (DDMMAAAA)")

CURSOR=CCALCCANT SELECT SUM(CASE WHEN BLOQUEOS='0000000000' THEN CANTIDAD ELSE 0 END) LIBRE,SUM(CASE WHEN BLOQUEOS='0000000000' THEN 0 ELSE CANTIDAD END) BLOQUEADO FROM VDSTOCK WHERE CODART=:CODART AND CODLOT=:CODLOT;


CAMPO=CODART,TOOLTIP("Código del artículo"),NOUPDATE,POSTCHANGE=FDESIGNACION("CSELDESART","No existe artículo"),TITULO("Artículo")
CAMPO=DESART,AUXILIAR,NOENTER,TOOLTIP("Designación del artículo"),TITULO(""),WHERE=CODART IN (SELECT CODART FROM VDARTIC WHERE DESART LIKE :DESART||'%');
CAMPO=CODLOT,POSTCHANGE=FVALNOCERO("Debe informar un lote"),NOUPDATE,TOOLTIP("Lote"),TITULO("Lote")
CAMPO=CODLOTPROV,TOOLTIP("Lote del proveedor"),TITULO("Lote Prov.")
CAMPO=CADUCI,TOOLTIP("Caducidad en formato DDMMAAAA"),TITULO("Caduci"),CONVIERTE=FCADUCIFROMDB;FCADUCI2DB
CAMPO=FORMATOOK,AUXILIAR,OCULTO,"_8_"
CAMPO=RETEST,TOOLTIP("Fecha de retest del lote en formato DDMMAAAA"),TITULO("Retest"),UPDATE("IMPLANTADOR")
CAMPO=BLOQLOTE,NOUPDATE("CALIDAD"),TOOLTIP("Bloqueos del lote"),POSTCHANGE=FDESIGNACION("-CVERBLOQUEO",":V10ERROR"),TITULO("Bloqueos"),CONVIERTE=FCADUCIFROMDB;FCADUCI2DB,WLONX=10
CAMPO=SWTFMD,TITULO("FMD"),TOOLTIP("El lote del artículo está sujeto a FMD")
CAMPO=LIBRE,AUXILIAR,NOENTER,NOUPDATE,TITULO("Libre")
CAMPO=BLOQUEADO,AUXILIAR,NOENTER,NOUPDATE,TITULO("Bloq.")
CAMPO=POTENCIA,TOOLTIP("Potencia del lote"),TITULO("Potencia"),CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART")
CAMPO=MUESTREADO,TOOLTIP("Indica si se ha muestreado ese lote"),TITULO("Muestreado"),WLONX=10
CAMPO=VDEXTRA,TOOLTIP("Campo extra para el implantador"),TITULO("Extra")
CAMPO=CODCOMEN,NOENTER,TOOLTIP("Código de comentario del lote"),TITULO("Comen")
CAMPO=CODOPEMODIF,NOENTER,TOOLTIP("Operario que realizó la última modificación"),TITULO("Ultima modificación")
CAMPO=FECMODIF,NOENTER,TOOLTIP("Fecha en que se modificaron los datos del registro")
CAMPO=HORAMODIF,NOENTER,TOOLTIP("Hora en que se modificaron a los datos del registro")
CAMPO=RET,AUXILIAR,OCULTO,"@@@"
CAMPO=MENUCONTEXTUAL,NOUPDATE,AUXILIAR,OCULTO,"_20_"

CURSOR=CSELMODIF SELECT VDUSER.GETUSER CODOPEMODIF,VD.FECHASYS FECMODIF,VD.HORASYS HORAMODIF,'N' MUESTREADO FROM DUAL;

CURSOR=CSELDESART SELECT DESART FROM VDARTIC WHERE CODART=:CODART;

CURSOR=CVERBLOQUEO SELECT VDART.VALIDABLOQUEO(:BLOQLOTE) V10ERROR FROM DUAL WHERE VDART.VALIDABLOQUEO(:BLOQLOTE) IS NOT NULL AND :BLOQLOTE IS NOT NULL;

CURSOR=CSELBLOQUEO SELECT '0' || DECODE(CLA.CODCLASE,'CUARENTENA','2','0') || '00000000' BLOQLOTE
                     FROM VDCLASEARTIC CLA
                    WHERE CLA.CODART = :CODART AND CLA.CODCLASIF='RECEPCION';

CURSOR=CCHECKFORMATOCADUCI SELECT TO_CHAR(TO_DATE(:CADUCI,'YYYYMMDD'),'YYYYMMDD') FORMATOOK FROM DUAL WHERE :CADUCI IS NOT NULL AND TO_NUMBER(SUBSTR(:CADUCI,1,4))> 2000;

CURSOR=CCHECKFORMATORETEST SELECT TO_CHAR(TO_DATE(:RETEST,'YYYYMMDD'),'YYYYMMDD') FORMATOOK FROM DUAL WHERE :RETEST IS NOT NULL AND TO_NUMBER(SUBSTR(:RETEST,1,4))> 2000;

CURSOR=CSELRETEST SELECT DECODE(UNIRETEST,'A',TO_CHAR(ADD_MONTHS(TO_DATE(:CADUCI,'YYYYMMDD'),-12*TRETEST),'YYYYMMDD'),
                                          'M',TO_CHAR(ADD_MONTHS(TO_DATE(:CADUCI,'YYYYMMDD'),-TRETEST),'YYYYMMDD'),
                                          'D',TO_CHAR(TO_DATE(:CADUCI,'YYYYMMDD')-TRETEST,'YYYYMMDD')) RETEST
                    FROM VDARTIC 
                   WHERE CODART = :CODART AND UNIRETEST IN ('A','M','D');

CURSOR=CSELPOTENCIA SELECT DECODE(:POTENCIA,0,1,:POTENCIA) POTENCIA FROM DUAL;

CURSOR=CIMPREETIQ DECLARE
                    MICODIMPRE VARCHAR2(100);
                  BEGIN
                    VDIMPRIME.IMPRIME('ETIQ','VDARTIC.ZPL;VDETIART.VSQ;CSELART','CODART='||:CODART||';CODLOT='||:CODLOT,1,MICODIMPRE,'',:RET);    
                    COMMIT;
                  END;@

CURSOR=CHECKERROR SELECT :RET FROM DUAL WHERE :RET=0; 

CURSOR=CPRIVILEGIO SELECT :CODART FROM DUAL WHERE VDUSER.TENGOPRIVILEGIO('ADMINISTRADOR')=1;

CURSOR=CPRIVILEGIOQA SELECT :CODART FROM DUAL WHERE VDUSER.TENGOPRIVILEGIO('CALIDAD')=1;

CURSOR=CDAMEPRIVILEGIO SELECT CASE 
                               WHEN VDUSER.TENGOPRIVILEGIO('ADMINISTRADOR')=1 THEN 'VDLOTESADMIN.ZOO'
                               WHEN VDUSER.TENGOPRIVILEGIO('CALIDAD')=1 THEN 'VDLOTESQA.ZOO'
                               ELSE 'VDLOTES.ZOO'
                             END MENUCONTEXTUAL
                     FROM DUAL; 

#CONTEXTUAL=FIF("CPRIVILEGIO",FLEEMENU("VDLOTESADMIN.ZOO"),FEJECUTA(FIF("CPRIVILEGIOQA",FLEEMENU("VDLOTESQA.ZOO"),FLEEMENU("VDLOTES.ZOO"))))
#CONTEXTUAL=FEJECUTA("CDAMEPRIVILEGIO","",FLEEMENU(":MENUCONTEXTUAL"))
CONTEXTUAL=FEJECUTA(FLEEMENU("VDLOTES.ZOO"))


TECLA=CF1,FEJECUTA("CIMPREETIQ","CIMPREETIQ","CHECKERROR","ERROR :RET")
