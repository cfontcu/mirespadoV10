IDENTIFICACION1

 Código   _15____________

 Password _P________


                          |

TABLA=DUAL
NOQUERY
NOLOCK
NOCOMMIT


TECLA=F4,FEJECUTA(FVALIDOBLOQUE,"DEBE INTRODUCIR DATOS","+CPASAMAY","No puedo pasar el password a mayusculas",
                  FIF(FVERIFICAPASS(":CODOPE",":PASSWORD","MENSAJE"),
                       FEJECUTA(FPONNIVEL,"",
                                "+CSELULTLOGIN","ULTIMO LOGIN EL :ULTLOGIN",
                                "+CSELINCLOGIN","",
                                "%CVERLOGIN","ULTIMO LOGIN EL :ULTLOGIN",
                                "+CINSLOGCORR","Error al insertar en login",
                                "+CSELTEMACOLOR","",
                                FCARGATEMA(":TEMA"),"ERROR CARGANDO COLORES",
                                FCOMMIT,"",FPULSATECLAS("ESC"),""),
                       FEJECUTA("+CINSLOGERR","Error al insertar en login",
                                FCOMMIT,"",
                                "DAMEPROPIEDADES","ERROR AL VER CONFIGURACION DEL PASSWORD",
                                FIF("-CSUPMAXLOGIN",
                                    FEJECUTA("CUPDABLOQUSU","ERROR AL BLOQUEAR EL USUARIO",
                                             FCOMMIT,"",
                                             FEXPULSAUSUARIO,"")),"AQUI NO ESTOY",
                                %FFAILURE,"PASSWORD INCORRECTO\n:MENSAJE")),"")

CAMPO=CODOPE,MIXED,TOOLTIP("Código de usuario en V10"),
                POSTCHANGE=FEJECUTA("CSELUSER","No existe usuario","CSELBLOQ","Usuario bloqueado")
CAMPO=PASSWORD,MIXED,AUXILIAR,TOOLTIP("Contraseña del usuario en V10"),TECLA=CR,FPULSATECLAS("F4")
CAMPO=MENUASOCIADO,OCULTO,"________________________________________"
CAMPO=IDIOMA,OCULTO,"____"
CAMPO=NIVEL,OCULTO,"@L"
CAMPO=MENSAJE,AUXILIAR,OCULTO,"_500"
CAMPO=ERRSUC,AUXILIAR,VIRTUAL,OCULTO
CAMPO=PAMAXERRSUC,AUXILIAR,OCULTO,"@L"
CAMPO=PAMAXERRBLOQ,AUXILIAR,OCULTO,"@L"
CAMPO=ULTLOGIN,AUXILIAR,OCULTO,"_100"
CAMPO=FECULTLOGIN,AUXILIAR,OCULTO,"¿D-MM-Y.YY"
CAMPO=HORAULTLOGIN,AUXILIAR,OCULTO,"________"
CAMPO=NUMINCLOGIN,AUXILIAR,OCULTO,"@L"
CAMPO=TEMA,AUXILIAR,OCULTO,"_20_"
CAMPO=SUERROR,AUXILIAR,OCULTO,"_256_"

CURSOR=CVERLOGIN SELECT :ULTLOGIN FROM DUAL WHERE VD.GETPROP('PAVISOLOGIN')!='S';

CURSOR=CPASAMAY SELECT UPPER(:CODOPE) CODOPE,UPPER(:PASSWORD) PASSWORD FROM DUAL WHERE VD.GETPROP('PAADMITEMINUS')='N';

CURSOR=DAMEPROPIEDADES BEGIN
                         :PAMAXERRSUC:=VD.GETPROP('PAMAXERRSUC');
                         :PAMAXERRBLOQ:=VD.GETPROP('PAMAXERRBLOQ');
                       END;@

CURSOR=CSELUSER SELECT MENUASOCIADO,IDIOMA,NIVEL FROM VDUSUARIO WHERE CODOPE=DECODE(VD.GETPROP('PAADMITEMINUS'),'N',UPPER(:CODOPE),:CODOPE);

CURSOR=CSELBLOQ SELECT :CODOPE FROM VDUSUARIO WHERE CODOPE=DECODE(VD.GETPROP('PAADMITEMINUS'),'N',UPPER(:CODOPE),:CODOPE) AND BLOQUEOPE='N';

CURSOR=CINSLOGERR BEGIN
              INSERT INTO VDLOGIN(CODOPE,CORRECTO,FECLOGIN,HORALOGIN,MACHINE,PROGRAM)
                     SELECT :CODOPE,'N',VD.FECHASYS,VD.HORASYS,MACHINE,PROGRAM
                       FROM SYS.V_$SESSION
                       WHERE AUDSID=USERENV('SESSIONID');
              UPDATE VDUSUARIO SET CONTERRSUC=CONTERRSUC+1,CONTERRTOT=CONTERRTOT+1
                WHERE CODOPE=:CODOPE;
         END;@

CURSOR=CINSLOGCORR BEGIN
              INSERT INTO VDLOGIN(CODOPE,CORRECTO,FECLOGIN,HORALOGIN,MACHINE,PROGRAM)
                     SELECT :CODOPE,'S',VD.FECHASYS,VD.HORASYS,MACHINE,PROGRAM
                       FROM SYS.V_$SESSION
                       WHERE AUDSID=USERENV('SESSIONID');
              UPDATE VDUSUARIO SET CONTERRSUC=0,CONTLOG=CONTLOG+1
                WHERE CODOPE=:CODOPE;
      END;@

CURSOR=CSUPMAXLOGIN SELECT :ERRSUC+1 ERRSUC FROM DUAL WHERE :ERRSUC<:PAMAXERRSUC-1;

CURSOR=CSELULTLOGIN SELECT * 
                     FROM (SELECT TO_CHAR(TO_DATE(LOG.FECLOGIN,'J'),'DD-MM-YY')||' '||VDTRAD.TRADN(USU.IDIOMA,'A LAS',LOG.HORALOGIN,'\n\nEN ORDENADOR',LOG.MACHINE) ULTLOGIN,
                                  LOG.FECLOGIN FECULTLOGIN,LOG.HORALOGIN HORAULTLOGIN,USU.IDIOMA
                            FROM VDLOGIN LOG,VDUSUARIO USU
                            WHERE USU.CODOPE=:CODOPE AND LOG.CODOPE=USU.CODOPE AND LOG.CORRECTO='S'
                            ORDER BY LOG.FECLOGIN DESC,LOG.HORALOGIN DESC) 
					 WHERE ROWNUM=1;

CURSOR=CSELINCLOGIN SELECT :ULTLOGIN ||VDTRAD.TRADN(:IDIOMA,'\n\nNUMERO DE INTENTOS INCORRECTOS',COUNT(*)) ULTLOGIN
                     FROM VDLOGIN LOG
                     WHERE LOG.CODOPE=:CODOPE AND LOG.CORRECTO='N' AND LOG.FECLOGIN>=:FECULTLOGIN AND (LOG.HORALOGIN>=:HORAULTLOGIN OR LOG.FECLOGIN>:FECULTLOGIN);

CURSOR=CUPDABLOQUSU UPDATE VDUSUARIO SET BLOQUEOPE='S'
                     WHERE CODOPE=:CODOPE AND CONTERRSUC>=:PAMAXERRBLOQ;

CURSOR=CSELTEMACOLOR SELECT NVL(TEMA,'ESTANDAR') TEMA FROM VDUSUARIO WHERE CODOPE = :CODOPE;

TECLA=3000,FPULSATECLAS("CR","F4")

BOTON=prueba1,430,345,75,25,"LOGIN",NO,3000
TECLA=CUA,FPULSATECLAS("CR")
TECLA=CUU,FPULSATECLAS("STAB")

