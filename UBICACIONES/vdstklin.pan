# Módulo   : VDSTOCK.PAN
# Función  : Mantenimiento de stock
#
# Creación : 01-10-2007
# Autor    : EAA
###########################################
# Histórico de cambios:
MANTENIMIENTO DE STOCK
Orden Lote        Serie   Cantidad  Uni/emb E/con Bloqueos   Tipo     Recepcion    Actividad       Coment.   Ultima modificación
@L@@@ _40________ _60____ #L15##### #L15### #L### __________ _10_____ _20_________ _255___________ @L@@@@@@@ _32________ ¿D-MM-Y.YY  ________
|
TABLA=VDSTOCK
ORDERBY=ORDENSTK;

PREINSERT=FEJECUTA("CSELMODIF","ERROR EN FECHA DE MODIFICACION")
PREUPDATE=FEJECUTA("CSELMODIF","ERROR EN FECHA DE MODIFICACION")
POSTQUERY=FEJECUTA("CSELDECIMALES","ERROR AL CALCULAR DECIMALES")



CAMPO=CODMAT,VIRTUAL,OCULTO
CAMPO=ORDENSTK,TOOLTIP("Orden de la linea de stock en el contenedor"),TITULO("Orden"), WLONX=20
CAMPO=CODART,VIRTUAL,OCULTO
CAMPO=CODLOT,NOENTER,TITULO("Lote")
CAMPO=NUMEROSERIE,NOENTER,TITULO("Serie")
CAMPO=CANTIDAD,TOOLTIP("Cantidad en unidades host"),CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART"),TITULO("Cantidad")
CAMPO=UNIEMB,TOOLTIP("Unidades por embalaje en unidades host"),CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART"),TITULO("Uni/Emb"), WLONX=19
CAMPO=EMBCONT,TOOLTIP("Embalajes por contenedor"),TITULO("Emb/C"),WLONX=18
CAMPO=BLOQUEOS,TOOLTIP("Bloqueos del stock"),POSTCHANGE=FDESIGNACION("-CVERBLOQUEO",":ERROR"),TITULO("Bloqueos")
CAMPO=TIPOEMBA,TOOLTIP("Tipo de embalaje"),POSTCHANGE=FDESIGNACION("CSELTIPOEMBA","No existe tipo de embalaje"),TITULO("Tipo")
CAMPO=CODRECEP,TOOLTIP("Código de recepción de la línea de stock"),TITULO("Recep")
CAMPO=MARCASTK,TOOLTIP("Actividad"),TITULO("Actividad")
CAMPO=CODCOMEN,TOOLTIP("Código de comentario del contenedor"),TITULO("Comen")
CAMPO=CODOPEMODIF,NOENTER,TOOLTIP("Operario que realizó la última modificación"),TITULO("Ultima modificación")
CAMPO=FECMODIF,NOENTER,TOOLTIP("Fecha en que se modificaron los datos del registro")
CAMPO=HORAMODIF,NOENTER,TOOLTIP("Hora en que se modificaron a los datos del registro")
CAMPO=ERROR,AUXILIAR,OCULTO,"_255"

CURSOR=CSELMODIF SELECT VDUSER.GETUSER CODOPEMODIF,VD.FECHASYS FECMODIF,VD.HORASYS HORAMODIF FROM DUAL;

CURSOR=CSELDESART SELECT DESART,DECIMALES CANTIDAD__DECIMALES,DECIMALES UNIEMB__DECIMALES FROM VDARTIC WHERE CODART=:CODART;

CURSOR=CSELCODLOT SELECT :CODLOT FROM VDLOTES WHERE CODART=:CODART AND CODLOT=:CODLOT;

CURSOR=CSELTIPOEMBA SELECT :TIPOEMBA FROM VDTIPOEMBA WHERE TIPOEMBA=:TIPOEMBA;

CURSOR=CVERBLOQUEO SELECT VDART.VALIDABLOQUEO(:BLOQUEOS) ERROR FROM DUAL WHERE VDART.VALIDABLOQUEO(:BLOQUEOS) IS NOT NULL;

CURSOR=CSELDECIMALES SELECT DECIMALES CANTIDAD__DECIMALES,DECIMALES UNIEMB__DECIMALES
                      FROM VDARTIC
                      WHERE CODART=:CODART;




