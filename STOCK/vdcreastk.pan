###################################################################
#Módulo: VDCREASTK.PAN
#Funcionalidad :  Creación de nuevo stock desde pantalla
#Autor: FGS      
#Fecha: 22-07-2008
###################################################################
# Histórico de cambios:
CREACIÓN DE STOCK DESDE PANTALLAS
   
   Matrícula:   __________________    
                                                                                
   Artículo:    _40___________________  
     Desc:      _100_________________________________________________
   Lote:        _40__________________  __________  _________            
  
   UnixEmb:     #L15#.##    EmbxCont:    #L######  Embalaje:    __________  _40_________________
  
   Núm.Serie:   _60_______________  
   Bulto:       __________________  
   MarcaStock:  _60_______________  
   Recepción:   ____________________
   Precio Unitario:   #L12#####.###    Peso Unitario:   #L######.### gr    
   
   Cantidad: #L######.###    Insertar antes: _ Concepto: _10________

  Observ: 
   _1024______________________________________________________________________________________
   _1024______________________________________________________________________________________
   _1024______________________________________________________________________________________
   _1024______________________________________________________________________________________
|

NOQUERY
PREREGISTRO=FEJECUTA("CSELOPE","")

CURSOR=CSELOPE SELECT VDUSER.GETUSER MICODOPE FROM DUAL;

CAMPO=MICODOPE,AUXILIAR,OCULTO,"_20"

CAMPO=CODMAT,VIRTUAL,NOENTER,POSTCHANGE=FEJECUTA("CSELDATOSACT","ERROR OBTENIENDO DATOS DE LA UBICACION ACTUAL")
CAMPO=CODART,VIRTUAL,PROTECT("ADMINISTRADOR"),AUXILIAR,POSTCHANGE=FDESIGNACION("CSELDESART","ARTÍCULO :CODART NO EXISTE",
                                              "CSELDATOS","ERROR OBTENIENDO DATOS DEL ARTICULO",
                                              "CSELDATOSACT","ERROR OBTENIENDO DATOS DE LA UBICACION ACTUAL",
                                              "CSELMULTIART"," ARTICULO :CODART DIFERENTE DEL ARTICULO :CODARTACT QUE TIENE LA UBICACION\n UBICACION MULTIARTICULO = :MULTIARTICULO",
											  "CESCOMPART","ARTICULO INCOMPATIBLE CON LA UBICACION"),
                      CONTEXTUAL=FEJECUTAFORM("VDSELART","N","","","CODART=:CODART","SELECCIÓN DE ARTICULOS","S")
CAMPO=DESART,VIRTUAL,AUXILIAR,NOENTER
CAMPO=CODLOT,PROTECT("ADMINISTRADOR"),AUXILIAR,POSTCHANGE=FDESIGNACION("CSELDESART","ARTÍCULO :CODART NO EXISTE",
                                              "CSELDATOS","ERROR OBTENIENDO DATOS DEL ARTICULO",
											  "CESCOMPART","ARTICULO INCOMPATIBLE CON LA UBICACION",
                                              "CSELDATOSACT","ERROR OBTENIENDO DATOS DE LA UBICACION ACTUAL",
                                              "CSELMULTIART"," ARTICULO :CODART DIFERENTE DEL ARTICULO :CODARTACT QUE TIENE LA UBICACION\n UBICACION MULTIARTICULO = :MULTIARTICULO",
                                              "CSELLOTE","LOTE :CODLOT NO EXISTE",
                                              "+CSELLIB","LOTE :CODLOT NO EXISTE",
                                              "CSELMULTILOTE"," LOTE :CODLOT DIFERENTE DEL LOTE :CODLOTACT QUE TIENE LA UBICACION\n UBICACION MULTIFORMATO = :MULTIFORMATO"),
                      CONTEXTUAL=FEJECUTAFORM("VDLOTES","N","CODART=:CODART","","CODLOT=:CODLOT","SELECCIÓN LOTES DEL ARTICULO :CODART","S"),COMBOX("CLISTALOTES")
CAMPO=BLOQLOTE,AUXILIAR,NOENTER
CAMPO=CADUCI,AUXILIAR,NOENTER
CAMPO=UNIEMB,PROTECT("ADMINISTRADOR"),AUXILIAR,
                      TOOLTIP("Unidades por embalaje en unidades host"),
                              POSTCHANGE=FVALNOCERO("Las unidades por embalaje no pueden ser 0")
CAMPO=EMBCONT,PROTECT("ADMINISTRADOR"),AUXILIAR,POSTCHANGE=FVALNOCERO("Los Embalajes por Contenedor no pueden ser 0")
CAMPO=TIPOEMBA,PROTECT("ADMINISTRADOR"),AUXILIAR,POSTCHANGE=FDESIGNACION("CDESTIPOEMBA","TIPO DE EMBALAJE :TIPOEMBANUEVO NO EXISTE"),
                                CONTEXTUAL=FEJECUTAFORM("VDTIPOEMBA","N","","","TIPOEMBA=:TIPOEMBA","SELECCIÓN DE TIPO DE EMBALAJE","S")
CAMPO=DESTIPOEMBA,AUXILIAR,NOENTER                                
CAMPO=NUMEROSERIE,PROTECT("ADMINISTRADOR"),AUXILIAR
CAMPO=BULTO,PROTECT("ADMINISTRADOR"),AUXILIAR
CAMPO=MARCASTK,PROTECT("ADMINISTRADOR"),AUXILIAR
CAMPO=CODRECEP,PROTECT("ADMINISTRADOR"),AUXILIAR,POSTCHANGE=FEJECUTA("+CVERIFCODRECEP","NO EXISTE RECEPCION :CODRECEP")
CAMPO=PRECIOUNI,PROTECT("ADMINISTRADOR"),AUXILIAR,TOOLTIP("Precio unitario del producto en unidades host")
CAMPO=PESOUNI,PROTECT("ADMINISTRADOR"),AUXILIAR,TOOLTIP("Peso unitario del producto en gramos")
CAMPO=DIFF,PROTECT("ADMINISTRADOR"),AUXILIAR,TOOLTIP("Cantidad a Regularizar")
CAMPO=BOCANAL,AUXILIAR,CHECKBOX("N","S")
CAMPO=CODCONCE,PROTECT("ADMINISTRADOR"),AUXILIAR,CONTEXTUAL=FEJECUTA("CCHECKART","PARA SELECCIONAR EL CONCEPTO DEBES INTRODUCIR ARTÍCULO",
                  FEJECUTAFORM("VDSELCONCEPTOFORM2","N","","CAMPO='CREAR' CODART=:CODART CANTIDAD=:DIFF","CODCONCE=:CODCONCEENT UBICODCONCE=:FUENTE ORDENCONCEPTO=:ORDEN CANTMAX=:CANTMAX PVPMAX=:PVLMAX","SELECCIÓN DE CONCEPTOS PARA CREACIÓN DE STOCK","S")),
                  COMBOX("CLISTACONCE"),POSTCHANGE=FEJECUTA("CSELORDEN","No existe concepto de entrada")

#FEJECUTAFORM("VDSELCONCEPTOFORM2","N","","CAMPO='CREAR' CODART=:CODART","CODCONCE=:CODCONCEENT UBICODCONCE=:FUENTE ORDENCONCEPTO=:ORDEN CANTMAX=:CANTMAX PVPMAX=:PVLMAX","SELECCIÓN DE CONCEPTOS PARA CREACIÓN DE STOCK","S")
#FEJECUTAFORM("VDSELCONCEPTOFORM2","N","","CODART=:CODART CAMPO='CREAR' CANTIDAD=:DIFF","CODCONCE=:CODCONCEENT UBICODCONCE=:FUENTE CANTMAX=:CANTMAX PVPMAX=:PVLMAX","SELECCION DE CONCEPTOS PARA REGULARIZACIONES DE CANTIDAD","S")
                  
CAMPO=UBICODCONCE,AUXILIAR,OCULTO,"_12_____"
# OBSERVACIONES
CAMPO=OBSERV1,AUXILIAR
CAMPO=OBSERV2,AUXILIAR
CAMPO=OBSERV3,AUXILIAR
CAMPO=OBSERV4,AUXILIAR
CAMPO=OBSERV,AUXILIAR,OCULTO,"_4096______"
CAMPO=CANTMAX,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=PVPMAX,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=PVPDIFF,AUXILIAR,OCULTO,"@L@@@@@@@@@@@@@@@"
CAMPO=ORDENCONCEPTO,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=ORDENSTK,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=CODMOVOUT,AUXILIAR,OCULTO,"@L@@@@@@@"

CAMPO=CODARTACT,AUXILIAR,OCULTO,"_40_"
CAMPO=CODLOTACT,AUXILIAR,OCULTO,"_40_"
CAMPO=MULTIFORMATO,AUXILIAR,OCULTO,"_"
CAMPO=MULTIARTICULO,AUXILIAR,OCULTO,"_"
CAMPO=DUMMY,AUXILIAR,OCULTO,"_"
CAMPO=MIORDENSTK,VIRTUAL,AUXILIAR,OCULTO


CURSOR=CSELORDEN SELECT ORDEN ORDENCONCEPTO FROM VDCONCEPTOFORM WHERE TIPOPROP='CREAR' AND CODCONCEENT=:CODCONCE;


CURSOR=CLISTALOTES SELECT LOT.CODLOT LOTE,LOT.CADUCI CADUCIDAD
                      FROM VDLOTES LOT
					  WHERE LOT.CODART=:CODART AND ROWNUM<1000
					  GROUP BY LOT.CODLOT,LOT.CADUCI;



CURSOR=CLISTACONCE SELECT CODCONCEENT,DESTIPOPROP,FUENTE FROM VDCONCEPTOFORM CFRM
                            WHERE  TIPOPROP='CANTIDAD' AND
                                   ACTIVO='S' AND
                                   VDUSER.TENGOPRIVILEGIO(PRIVILEGIOS)=1 AND
                                   (CODCLASE IS NULL OR EXISTS (SELECT 1 FROM VDCLASEARTIC CLA WHERE CLA.CODCLASIF=CFRM.CODCLASIF AND CLA.CODCLASE=CFRM.CODCLASE AND CLA.CODART=:CODART)) AND
                                   (CFRM.RANGODIV IS NULL OR EXISTS (SELECT 1 FROM VDARTIC ART WHERE ART.CODART=:CODART AND VD.CONTIENECAD(CFRM.RANGODIV,ART.CODDIV)>0))
                                   ORDER BY ORDEN;

CURSOR=CSELDATOSACT SELECT STK.CODART CODARTACT,STK.CODLOT CODLOTACT,UBI.MULTIARTICULO,UBI.MULTIFORMATO 
                     FROM VDSTOCK STK , VDCONTE CNT, VDUBICA UBI 
                     WHERE STK.CODMAT(+)=CNT.CODMAT
                     AND CNT.CODUBI=UBI.CODUBI
                     AND CNT.CODMAT=:CODMAT;

CURSOR=CSELMULTIART SELECT 1 DUMMY FROM DUAL WHERE (:CODARTACT=:CODART) OR (:CODARTACT!=:CODART AND :MULTIARTICULO='S') OR (:CODARTACT IS NULL);
CURSOR=CSELMULTILOTE SELECT 1 DUMMY FROM DUAL WHERE (:CODLOTACT=:CODLOT) OR (:CODLOTACT!=:CODLOT AND :MULTIFORMATO='S') OR (:CODLOTACT IS NULL);


CURSOR=CSELDATOS SELECT 0 ORDENCONCEPTO, NULL CODCONCE,NULL UBICODCONCE,UNIEMB, EMBCONT,0 PESOUNI, TIPOEMBADEF TIPOEMBA FROM VDARTIC WHERE CODART=:CODART;

CURSOR=CSELUBI SELECT CODUBI FROM VDCONTE WHERE CODMAT=:CODMAT;

CURSOR=CDAMEOBSERV SELECT TRIM(:OBSERV1) || ' ' || TRIM(:OBSERV2) || ' ' || TRIM(:OBSERV3) || ' ' || TRIM(:OBSERV4) OBSERV FROM DUAL;

CURSOR=CSELDESART SELECT DESART,'N' BOCANAL FROM VDARTIC WHERE CODART=:CODART;

CURSOR=CSELLOTE SELECT CADUCI, BLOQLOTE FROM VDLOTES WHERE CODART=:CODART AND CODLOT=:CODLOT;

CURSOR=CSELLIB SELECT DECODE(COMENTARIO,'S',SUBSTR(:BLOQLOTE,1,1)||'0'||SUBSTR(:BLOQLOTE,3),:BLOQLOTE) BLOQLOTE FROM VDCOMEN COM,VDLOTES LOT 
                 WHERE COM.CODCOMEN=LOT.CODCOMEN AND TIPOCOMEN='LIBERADO' AND CODART=:CODART AND CODLOT=:CODLOT;

CURSOR=CVERIFCODRECEP SELECT :CODRECEP FROM VDRECEPCAB WHERE CODRECEP=:CODRECEP OR :CODRECEP IS NULL;

CURSOR=CDESTIPOEMBA SELECT DESTIPOEMBA FROM VDTIPOEMBA WHERE TIPOEMBA=:TIPOEMBA;

CURSOR=CCHECKARTYLOTE SELECT :CODART FROM VDLOTES WHERE CODART=:CODART AND CODLOT=:CODLOT;

CURSOR=CSUPERACANTMAX SELECT :CANTMAX FROM DUAL WHERE :CANTMAX>0 AND ABS(:DIFF)>VDART.HOST2VD(:CANTMAX,:CODART);

CURSOR=CSUPERAPVPMAX SELECT (ART.PVPVIGENTE*ABS(:DIFF)) PVPDIFF FROM VDARTIC ART WHERE :PVPMAX>0 AND CODART=:CODART AND (ART.PVPVIGENTE*ABS(:DIFF)) > :PVPMAX;


CURSOR=CDIFFESCERO SELECT :DIFF FROM DUAL WHERE :DIFF = 0;

CURSOR=CCODCONCENULO SELECT :CODCONCE FROM DUAL WHERE :CODCONCE IS NULL;

CURSOR=CCHECKART SELECT :CODART FROM DUAL WHERE :CODART IS NOT NULL;

CURSOR=CDAMEORDENSTK SELECT NVL(MAX(ORDENSTK),0)+1 ORDENSTK FROM VDSTOCK WHERE CODMAT=:CODMAT;

CURSOR=CSELBOCANAL SELECT :BOCANAL FROM DUAL WHERE :BOCANAL='S';
CURSOR=CDAMEORDBOCA SELECT :MIORDENSTK ORDENSTK FROM DUAL;
CURSOR=CUPDORDENSTK UPDATE VDSTOCK SET ORDENSTK=ORDENSTK+1 WHERE CODMAT=:CODMAT AND ORDENSTK>=:MIORDENSTK;

CURSOR=CESCOMPART SELECT :CODART
                    FROM VDCONTE
                  WHERE CODMAT=:CODMAT AND VDCOMP.UBI2ART(CODUBI,:CODART)=0;

#TECLA=F9,FEJECUTA("CCHECKART","PARA SELECCIONAR EL CONCEPTO DEBES INTRODUCIR ARTÍCULO",
#                  FEJECUTAFORM("VDSELCONCEPTOFORM2","N","","CAMPO='CREAR' CODART=:CODART","CODCONCE=:CODCONCEENT UBICODCONCE=:FUENTE ORDENCONCEPTO=:ORDEN CANTMAX=:CANTMAX PVPMAX=:PVLMAX","SELECCIÓN DE CONCEPTOS PARA CREACIÓN DE STOCK","S"))                           


TECLA=F4,FEJECUTA("CCHECKARTYLOTE","NO EXISTE ARTICULO Y LOTE INTRODUCIDO",
                  "-CDIFFESCERO","DEBE INFORMAR LA CANTIDAD",
                  "-CCODCONCENULO"," DEBE INFORMAR UN CONCEPTO ",               
                  "-CSUPERACANTMAX","LA CANTIDAD A CREAR NO PUEDE SUPERAR\n LA MÁXIMA PERMITIDA POR ESTE CONCEPTO (:CANTMAX :UNIDADESHOST)",
                  "-CSUPERAPVPMAX","EL PRECIO DE LA CREACIÓN DE STOCK ES :PVPDIFF Y SUPERA\n EL MÁXIMO A CREAR (:PVPMAX), POR ESTE CONCEPTO",
                  "+CDAMEOBSERV","",
                  FIF ("CSELBOCANAL", FEJECUTA("CUPDORDENSTK","ERROR ACTUALIZANDO ORDEN A STOCK DE :CODMAT","CDAMEORDBOCA","ERROR OBTENENIENDO BOCA DE CANAL"),"CDAMEORDENSTK"),"ERROR OBTENIENDO NUEVO ORDENSTK PARA MATRÍCULA :CODMAT",
                  FCREASTK(":CODMAT",":ORDENSTK",":ORDENCONCEPTO",
                           ":CODART",":CODLOT",":NUMEROSERIE",":BULTO",":MARCASTK",
                           ":CODRECEP",":UNIEMB",":EMBCONT",":TIPOEMBA",":PRECIOUNI",":PESOUNI",
                           ":BLOQLOTE",":DIFF",":OBSERV","CODMOVOUT"),"Error :V10ERROR",
                  FCOMMIT,"",
                  FIMPRIME("INFORME","$(VDMOVIMIENTO)","CODMOV=:CODMOVOUT",""),"ERROR IMPRIMIENDO LISTADO DE MOVIMIENTO :V10ERROR",                  
#                  %FFAILURE,"CREACIÓN DE STOCK REALIZADA. MOVTO CREADO :CODMOVOUT",
                  +FPULSATECLAS("ESC","F3","F2"),"")
                                  
BOTON=B1,565,200,32,32,"P",miok.png,F4,"Confirmar ",INCLUDECSS="background-color: transparent;border-width:0px"
BOTON=B2,615,200,32,32,"P",minook.png,ESC,"Deshacer ",INCLUDECSS="background-color: transparent;border-width:0px"
                  

