# Módulo   : VDEJECUTAMOV.PAN
# Función  : Lsita de ejecución manual de movimientos
#
# Creación : 25-04-2008
# Autor    : RFD
###########################################
# Histórico de cambios:
LISTA DE MOVIMIENTOS A CONFIRMAR
 Movimiento Concepto    Matrícula    Ubicación Artículo    Lote       Cantidad     Origen     Destino    Val. NoInter. 
 @L@@@@@@@  _32_______  _18________ _12_______ _20________ _10_______ #15L######## __________ __________  _    _
|

TABLA=VDMOVIM

REGPAG=30
PQUERY

NOINSERT
NOUPDATE,PROTECT("CONFIRMAMOV")
NODELETE
WHERE=STATUS=5100 AND SUBSTR(BLOQUEOSORI,2,1)!='3' AND SUBSTR(BLOQUEOSDEST,2,1)!='3';

POSTQUERY=FEJECUTA("CSELPONN","")

PREUPDATE=FEJECUTA(FIF ("CEJECUTAR",FEJECUTA(
                   "CSELMODIF","ERROR EN FECHA DE MODIFICACION",
                   "CSELCANTMAX","ERROR OBTENIENDO DATOS DE CANTIDAD",
                   "CSELPVLMAX","ERROR OBTENIENDO DATOS DE PRECIO",
                   FIF("CSUPERACANTMAX","CVERPRIVILEGIOSUP","CVERPRIVILEGIO"), "LA CANTIDAD SUPERA LA MÁXIMA PERMITIDA PARA SU PERFIL",
                   FIF("CSUPERAPVPMAX", "CVERPRIVILEGIOSUP","CVERPRIVILEGIO"), "EL PRECIO DEL AJUSTE SUPERA EL MÁXIMO PARA SU PERFIL",
                   "CUPDMOV","ERROR CONFIRMANDO MOVIMIENTO"),FEJECUTA("@CANULAR","","CANULMOV","")),"",
                   FPULSATECLAS("F5","F2"),"ERROR LIMPIANDO PANTALLA")



CAMPO=CODMOV,NOUPDATE,TITULO("Movimiento")
CAMPO=CODCONCE,NOENTER,TITULO("Concepto")
CAMPO=CODMATORI,NOENTER,TITULO("Matrícula")
CAMPO=CODUBIORI,NOENTER,TITULO("Origen")
CAMPO=CODART,NOENTER,TOOLTIP(":DESART"),POSTCHANGE=FDESIGNACION("CSELDESART","No existe el artículo"),TITULO("Artículo")
CAMPO=DESART,AUXILIAR,OCULTO,"_60_"
CAMPO=CODLOT,NOENTER,TOOLTIP("Lote"),TITULO("Lote")
CAMPO=CANTIDAD,NOENTER,TOOLTIP("Cantidad en unidades host"),CONVIERTE=FCONVIERTEARTFROMDB(":CODART");FCONVIERTEART2DB(":CODART"),TITULO("Cantidad")
CAMPO=BLOQUEOSORI,NOUPDATE,TITULO("Bloq.Orig")
CAMPO=BLOQUEOSDEST,NOUPDATE,TITULO("Bloq.Dest")
CAMPO=EJECUTAR,PROTECT("CONFIRMAMOV"),UPPER,AUXILIAR,CHECKBOX("N","S"),POSTCHANGE=FVERIFICA("SN ","Valores aceptados son S,N y blanco"),TITULO("Val"),WLONX=20
CAMPO=ANULAR,PROTECT("CONFIRMAMOV"),UPPER,AUXILIAR,CHECKBOX("N","S"),POSTCHANGE=FVERIFICA("SN ","Valores aceptados son S,N y blanco"),TITULO("NoInter"),WLONX=30
CAMPO=CANTMAX,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=PVLMAX,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=PVPDIFF,AUXILIAR,OCULTO,"@L@@@@@@@"
CAMPO=CODOPE,OCULTO,"_32_",AUXILIAR
CAMPO=CODOPEMODIF,OCULTO,"_32_"
CAMPO=FECMODIF,OCULTO,"¿D-MM-Y.YY"
CAMPO=HORAMODIF,OCULTO,"_8_"
CAMPO=RET,AUXILIAR,OCULTO,"@L@@@@@"

CURSOR=CSELPONN SELECT 'N' EJECUTAR,'N' ANULAR FROM DUAL;

CURSOR=CSELDESSTATUS SELECT DESSTATUS FROM VDSTATUS WHERE STATUS=:STATUS;

CURSOR=CEJECUTAR SELECT :EJECUTAR FROM DUAL WHERE :EJECUTAR='S' AND :ANULAR='N';

CURSOR=CANULAR SELECT :ANULAR FROM DUAL WHERE :EJECUTAR='N' AND :ANULAR='S';

CURSOR=CSELDESART SELECT DESART FROM VDARTIC WHERE CODART=:CODART;


CURSOR=CSELMODIF SELECT VDUSER.GETUSER CODOPEMODIF,VDUSER.GETUSER CODOPE,VD.FECHASYS FECMODIF,VD.HORASYS HORAMODIF FROM DUAL;

CURSOR=CVERPRIVILEGIOSUP SELECT :CODOPE 
                       FROM VDUSUARIO 
                       WHERE CODOPE=:CODOPE AND VDUSER.TIENEPRIVILEGIO(:CODOPE,'CONFIRMAMOVSUP')=1;

CURSOR=CVERPRIVILEGIO SELECT :CODOPE 
                       FROM VDUSUARIO 
                       WHERE CODOPE=:CODOPE AND VDUSER.TIENEPRIVILEGIO(:CODOPE,'CONFIRMAMOV')=1;

CURSOR=CSELCANTMAX SELECT VALOR CANTMAX FROM VDCONFI WHERE GRUPOPROP='VALIDAMOV' AND PROPIEDAD='CANTMAX';

CURSOR=CSELPVLMAX SELECT VALOR PVLMAX FROM VDCONFI WHERE GRUPOPROP='VALIDAMOV' AND PROPIEDAD='PVLMAX';

CURSOR=CSUPERACANTMAX SELECT :CANTMAX FROM DUAL WHERE :CANTMAX>0 AND :CANTIDAD >:CANTMAX  ;

CURSOR=CSUPERAPVPMAX SELECT (ART.PVPVIGENTE*ABS ( :CANTIDAD ) ) PVPDIFF 
           FROM VDARTIC ART WHERE :PVLMAX >0 AND CODART= :CODART AND (ART.PVPVIGENTE*ABS( :CANTIDAD ) ) > :PVLMAX ;

CURSOR=CUPDMOV UPDATE VDMOVIM SET STATUS=5200,CODOPEMODIF=VDUSER.GETUSER,FECMODIF=VD.FECHASYS,HORAMODIF=VD.HORASYS
               WHERE CODMOV=:CODMOV AND STATUS=5100;

CURSOR=CANULMOV UPDATE VDMOVIM SET STATUS=6000,CODOPEMODIF=VDUSER.GETUSER,FECMODIF=VD.FECHASYS,HORAMODIF=VD.HORASYS
            WHERE CODMOV=:CODMOV AND STATUS=5100;


CURSOR=CCREACORREO BEGIN 
                      VDCORREOS.CORREO('ROCHE',NULL,'Confirmación de movimientos ' || TO_CHAR(SYSDATE,'DD/MM/YYYY HH24:MI:SS'),'MOVIMIENTO: ' || :CODMOV ||' ,ARTICULO: ' || :CODART|| ' ,LOTE: '||:CODLOT ||' ,CANTIDAD: '||:CANTIDAD || ', PRECIO: '||:PVPDIFF || ' requiere confirmación de máximo nivel',NULL,NULL,3,'text/plain; charset=ISO-8859-1',NULL,NULL,:V10ERROR,:RET);
                   END;@
 


CONTEXTUAL=FEJECUTAFORM("VDMOVIM","S","CODMOV=:CODMOV","","","Consulta del Movimiento :CODMOV","S")


ONLINE= {F1} Ayuda  {F2} Consulta   {F4} Ejecutar   {SF10} Zoom;
